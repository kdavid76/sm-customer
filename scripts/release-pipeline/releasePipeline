// Uses Declarative syntax to run commands inside a container.
def NEXT_RELEASE = ''
def NEXT_SNAPSHOT = ''

pipeline {
    agent { label 'kubeagent' }
    options {
        // This is required if you want to clean before build
        skipDefaultCheckout(true)
    }
    environment {
        GITHUB_PATH = "https://${env.GITHUB_APIKEY}@github.com/kdavid76/sm-customer.git"
        DEVELOPMENT_CONN_URL = "scm:git:https://${env.GITHUB_APIKEY}@github.com/kdavid76/sm-customer.git"
    }
    tools {
        jdk 'oracle-jdk-17'
        maven 'mvn-3.8.7'
        git 'default'
    }
    stages {
        stage('Checkout from Git') {
            steps {
                cleanWs()
                checkout([
                        $class: 'GitSCM',
                        branches: [[name: 'master']],
                        userRemoteConfigs: [[url: env.GITHUB_PATH]]
                ])

                sh('git config --global user.email "mikehammer1902@gmail.com"')
                sh('git config --global user.name "kdavid76"')
                sh('git checkout master')
            }
        }
        stage('Build release') {
            steps {
                script {
                    NEXT_RELEASE= calculateNextRelease("${RELEASE_THIS_AS}")
                }
                echo "CALCULATED NEXT RELEASE: ${NEXT_RELEASE}"
                changePomVersion("${NEXT_RELEASE}")

                sh ('mvn clean package')
            }
        }
        stage('Deploy release') {
            parallel {
                stage('Push to GitHub') {
                    steps {
                        sh('git add .')
                        sh("git commit -m 'SM-CUSTOMER: Release ${NEXT_RELEASE}'")
                        sh("git tag -a 'v@${NEXT_RELEASE}' -m 'Release ${NEXT_RELEASE}' HEAD")
                        sh('git push --follow-tags')
                    }
                }
                stage('Push to artifactory') {
                    steps {
                        sh ('mvn deploy')
                    }
                }
                stage('Push to dockerhub') {
                    steps {
                        withCredentials([usernamePassword(credentialsId: 'DockerHub_Credentials', passwordVariable: 'DOCKER_REGISTRY_PWD', usernameVariable: 'DOCKER_REGISTRY_USER')]) {
                            sh('cd sm-customer-services')
                            sh ('mvn jib:build')
                        }
                    }
                }
            }
        }
        stage('Build next snapshot') {
            steps {
                script {
                    NEXT_SNAPSHOT = calculateNextSnapshot("${RELEASE_THIS_AS}", "${NEXT_SNAPSHOT_AS}")
                }
                echo "CALCULATED NEXT SNAPSHOT: ${NEXT_SNAPSHOT}"
                changePomVersion("${NEXT_SNAPSHOT}")

                sh ('mvn clean package')
            }
        }
        stage('Deploy snapshot') {
            parallel {
                stage('Push snapshot to GitHub') {
                    steps {
                        sh('git add .')
                        sh("git commit -m 'SM-CUSTOMER: Next snapshot ${NEXT_SNAPSHOT}'")
                        sh('git push')
                    }
                }
            }
        }
    }
}

def calculateNextRelease(inputRelease) {
    def mavenPom = readMavenPom file: 'pom.xml'
    def currentRelease = mavenPom.version.split("-")[0]
    def nextRelease = inputRelease

    echo "NEXT RELEASE (if released from current version): ${currentRelease}"

    if ( !nextRelease ) {
        nextRelease = currentRelease;
    }

    return nextRelease
}

def calculateNextSnapshot(inputRelease, inputSnapshot) {
    def mavenPom = readMavenPom file: 'pom.xml'
    //The current version must be a release version
    def currentRelease = mavenPom.version.split("-")[0]
    def nextSnapshot = inputSnapshot;

    if( inputRelease ) {
        versionParts = inputRelease.split("\\.")
        def incVer = increaseAsInteger("${versionParts[2]}")
        nextSnapshot = getSnapshotVersionString("${versionParts[0]}","${versionParts[1]}", "${incVer}")
    } else {
        if( !nextSnapshot) {
            def versionParts = currentRelease.split("\\.")
            if( inputRelease ) {
                versionParts = inputRelease.split("\\.")
                def incVer = increaseAsInteger("${versionParts[2]}")
                nextSnapshot = getSnapshotVersionString("${versionParts[0]}","${versionParts[1]}", "${incVer}")
            } else if ( "${INCREMENT_FIELD}" == "Build Number" ) {
                def incVer = increaseAsInteger("${versionParts[2]}")
                nextSnapshot = getSnapshotVersionString("${versionParts[0]}","${versionParts[1]}", "${incVer}")
            } else if ( "${INCREMENT_FIELD}" == "Minor Version" ) {
                def incVer = increaseAsInteger("${versionParts[1]}")
                nextSnapshot = getSnapshotVersionString("${versionParts[0]}","${incVer}", "0")
            } else {
                def incVer = increaseAsInteger("${versionParts[0]}")
                nextSnapshot = getSnapshotVersionString("${incVer}","0", "0")
            }
        }
    }

    return nextSnapshot
}

def increaseAsInteger(value) {
    def incVer = "${value}" as Integer
    incVer++

    return incVer
}

def getSnapshotVersionString(partZero, partOne, partTwo) {
    return "${partZero}.${partOne}.${partTwo}-SNAPSHOT"
}

def changePomVersion(newVersion) {
    sh("mvn versions:set -DnewVersion=${newVersion} -P scm-release")
    sh("mvn versions:commit -P scm-release")
}
